/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_lharnob.skrit

	Author(s)	: 	Anu Kirk (ripping off Chad Queen)

	Purpose		: 	Lharnob (a1_r1) talk skrit

	(C)opyright 2003 Mad Doc Software, LLC/Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init

event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;  
}


////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{		
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );	
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		// game complete, thank the player
		if( Victory.IsQuestCompleted( "a1_staff", m_Target$ ) 
			|| Victory.GetQuestOrder( "a1_staff", m_Target$) > 10 )
		{
			m_Go$.GetConversation.RSSelectConversation( "lharnob_game_complete" );
			TryAnim$('talk');
		}
		// queen has been killed
		else if ( Victory.GetQuestOrder( "a1_staff", m_Target$) >= 2 )
		{
			m_Go$.GetConversation.RSSelectConversation( "lharnob_after_a1_monsters_2" );
			TryAnim$('talk');			
		}
		else if ( Victory.IsQuestCompleted( "a1_bandits" , m_Target$) 
		// TAT - if you speak to me before the Mayor, the quest isn't complete yet -
		//	so if you've killed the giant, we also get this dialogue
			|| Victory.GetQuestOrder( "a1_bandits", m_Target$ ) >= 3 )
		{
			
			m_Go$.GetConversation.RSSelectConversation( "lharnob_after_a1_monsters" );
			TryAnim$('talk');
		}
		// if the bandit quest is active and the giant hasn't been killed
		else if ( Victory.GetQuestOrder( "a1_bandits", m_Target$ ) < 3)
		{
			// we start with bandits active, so instead of inactive, check for current order
			if( Victory.GetQuestOrder( "a1_bandits", m_Target$ ) == 0 )
			{
				m_Go$.GetConversation.RSSelectConversation( "lharnob_before_a1_monsters" );
				TryAnim$('talk');
			}
			else
			{
				m_Go$.GetConversation.RSSelectConversation( "lharnob_during_a1_monsters" );
			 	TryAnim$('talk');
			}
		}
		else
		{
			m_Go$.GetConversation.RSSelectConversation( "lharnob_before_a1_monsters" );
			TryAnim$('talk');
		}
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}


