/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_ghage.skrit

	Author(s)	: 	Anu Kirk (ripping off Chad Queen)

	Purpose		: 	Ghage talk skrit for a7_zking

	(C)opyright 2003 Mad Doc Software, LLC/Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init

event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;  
}


////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{		
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );	
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('sit3');
	}

	event OnEnterState$
	{
		// if the player has ghage quest and a7_zking is completed or nossy has been killed
		if ( Victory.IsQuestActive( "a7_vfind", m_Target$ ) && 
		     (	GameAuditor.GetDb.GetBool("a7_zking_dead_710031c" ) || 
			Victory.IsQuestCompleted( "a7_zking" ) ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "ghage_after_a7_king_before_vfind_complete" );
			TryAnim$('sit3');
		}
		else if ( GameAuditor.GetDb.GetBool("a7_zking_dead_710031c" ) && !Victory.IsQuestCompleted( "a7_zking" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "ghage_hello_a7_king_dead" );
			TryAnim$('sit3');
		}
		else if ( Victory.IsQuestCompleted( "a7_zking" , m_Target$ ) )
		{
			 m_Go$.GetConversation.RSSelectConversation( "ghage_after_a7_zking" );
			 TryAnim$('sit3');
		}
		
		else if ( Victory.IsQuestActive( "a7_zking" , m_Target$ ) )
		{
			// if a7_vfind is active, then we spoke to Ghage BEFORE saving
			//	Umberteen, but now he's saved
			//	So say appropriate umberteen type stuff
			if ( Victory.IsQuestActive( "a7_vfind", m_Target$ )
				&& (Goid.InValidGoid != UIPartyManager.DoesActivePartyHaveTemplate( "book_glb_magic_umberteen", m_Target$ ) ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "ghage_after_a7_umberteen_has_a7_zking" );
			}
			else
			{
				m_Go$.GetConversation.RSSelectConversation( "ghage_during_a7_zking" );
			}
			
			TryAnim$('sit3');
		}		
		else
		{
			// if they have umberteen's book, pick the right conversation
			if ( Goid.InValidGoid != UIPartyManager.DoesActivePartyHaveTemplate( "book_glb_magic_umberteen", m_Target$ ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "ghage_before_a7_zking" );
			}	
			else
			{
				m_Go$.GetConversation.RSSelectConversation( "ghage_before_a7_zking_no_umb" );
			}
			TryAnim$('sit3');
		}
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}


