/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_konus.skrit

	Author(s)	: 	Bartosz Kijanka / Anu

	Purpose		: 	Make Konus (A4 NPC) talk goodly

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Face$;
}

////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
	
		// Several conversation options are independent of quest state
		// ACCEPT - he's joining the party
		if ( GameAuditor.GetDb.GetBool( "party_accept_0x3300035" ) ) 
		{
			// if this is during the quest we say a different accept thing
			if ( Victory.IsQuestActive( "a4_scourge", m_Target$ ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "konus_during_a4_scourge_accept" );
			}
			else
			{
				m_Go$.GetConversation.RSSelectConversation( "konus_accept" );
			}
			GameAuditor.GetDb.SetBool( "party_accept_0x3300035", false );
			TryAnim$('talk');
		}
		// reject - we don't want ya
		else if ( GameAuditor.GetDb.GetBool( "party_decline_0x3300035" ) ) 
		{
			m_Go$.GetConversation.RSSelectConversation( "konus_reject" );
			GameAuditor.GetDb.SetBool( "party_decline_0x3300035", false );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x3300035", true );
			TryAnim$('talk');
		}
		// disband
		else if ( GameAuditor.GetDb.GetBool( "party_disband_0x3300035" ) ) 
		{
			m_Go$.GetConversation.RSSelectConversation( "konus_disband" );
			GameAuditor.GetDb.SetBool( "party_disband_0x3300035", false );
			GameAuditor.GetDb.SetBool( "party_disbanded_0x3300035", true );
			TryAnim$('talk');
		}
		// join after disband
		else if ( GameAuditor.GetDb.GetBool( "party_disbanded_0x3300035" ) ) 
		{
			// should be rehire - we don't have one, just use accept
			m_Go$.GetConversation.RSSelectConversation( "konus_rejoin" );			
			TryAnim$('talk');
		} 
		else if ( GameAuditor.GetDb.GetBool( "party_allow_rejoin_0x3300035" ) ) 
		{
			m_Go$.GetConversation.RSSelectConversation( "konus_rejoin" );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x3300035", false );
			TryAnim$('talk');
		}
		// this is our first time saying hi, but we have already killed cicatrix
		else if ( 	!Victory.IsQuestActive( "a4_scourge", m_Target$) && 
				!Victory.IsQuestCompleted( "a4_scourge", m_Target$) &&
				GameAuditor.GetDb.GetBool("cicatrix_dead" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "konus_hello_cicatrix_dead" );			
			TryAnim$('talk');
		}
		// quest is complete or the cicatrix has been killed
		else if ( Victory.IsQuestCompleted( "a4_scourge", m_Target$ ) || GameAuditor.GetDb.GetBool("cicatrix_dead" ) )
		{			
			m_Go$.GetConversation.RSSelectConversation( "konus_hello_cicatrix_dead" );			
			TryAnim$('talk');
		}
		else if ( Victory.IsQuestActive( "a4_scourge", m_Target$ ) )
		{
			// saying hi DURING the quest
			m_Go$.GetConversation.RSSelectConversation( "konus_during_a4_scourge_hello" );			
			TryAnim$('talk');
		}
		else
		{
			// Just say hi
			m_Go$.GetConversation.RSSelectConversation( "konus_hello" );
			TryAnim$('talk');
		}
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


