/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk.skrit

	Author(s)	: 	Bartosz Kijanka

	Purpose		: 	Just stand there and talk at anyone we're told to.

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Face$;
}


////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		#only( game )
		[[

		// $$$ read/write variables from the Game Auditor here, and bring up the appropriate dialogs...
		m_Go$.GetConversation.RSSelectConversation( "lyssa_place_marker" );

		if ( GameAuditor.GetDb.GetBool( "beenhereagain_0x0010002D" ) )
		{
			OverheadMap.RSRemoveAllMarkers(m_Target$);
			GameAuditor.GetDb.SetBool( "beenhere_0x0010002D", false );
			GameAuditor.GetDb.SetBool( "beenhereagain_0x0010002D", false );

			m_Go$.GetConversation.RSSelectConversation( "lyssa_remove_markers" );
			TryAnim$('talk');
		} 
		else if ( GameAuditor.GetDb.GetBool( "beenhere_0x0010002D" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "lyssa_update_marker" );

			GameAuditor.GetDb.SetBool( "beenhereagain_0x0010002D", true );

			// set the test marker as the current one
			OverheadMap.RSUpdateCurrentAreaMarker("fire_swamp", m_Target$);
			// and make the text quest text
			OverheadMap.RSUpdateAreaMarkerQuestState("fire_swamp", true, m_Target$);

			TryAnim$('talk');
		} 
		else 
		{
			// try out the new marker adding thing
		//	OverheadMap.RSUpdateAreaMarkerVisibility("fire_swamp", true, m_Target$);
		//	OverheadMap.RSUpdateAreaMarkerQuestState("fire_swamp", false, m_Target$);

			GameAuditor.GetDb.SetBool( "beenhere_0x0010002D", true );

			TryAnim$('talk');
		}

		m_Go$.GetConversation.RSActivateDialogue();
		]]
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


