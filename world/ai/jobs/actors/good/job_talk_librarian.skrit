/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_algher.skrit

	Author(s)	: 	Bartosz Kijanka / Anu

	Purpose		: 	Make Algher Mhankur (a3_r3 NPC) talk goodly

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Face$;
}

////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		// $$$ read/write variables from the Game Auditor here, and bring up the appropriate dialogs...
		
		if( GameAuditor.GetDb.GetBool( "librarian_01" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "librarian_01" );
		}
		else if( GameAuditor.GetDb.GetBool( "librarian_02" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "librarian_02" );
		}
		else if( GameAuditor.GetDb.GetBool( "librarian_03" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "librarian_03" );
		}
		else if( GameAuditor.GetDb.GetBool( "librarian_04" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "librarian_04" );
		}
		else if( GameAuditor.GetDb.GetBool( "librarian_05" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "librarian_05" );
		}
		else if( GameAuditor.GetDb.GetBool( "librarian_06" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "librarian_06" );
		}

		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


