/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_kiira.skrit

	Author(s)	: 	Jason Dunlop

	Purpose		: 	syrd (a1_r1) talk skrit

	(C)opyright 2003 Mad Doc Software, LLC/Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init

event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;  
}


////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{		
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );	
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('sit3');
	}

	event OnEnterState$
	{
		// game complete, say something crazy
		if( Victory.IsQuestCompleted( "a1_staff", m_Target$ ) 
			|| Victory.GetQuestOrder( "a1_staff", m_Target$) > 10 )
		{
			m_Go$.GetConversation.RSSelectConversation( "syrd_game_complete" );
			TryAnim$('sit3');
		}
		// if bandits quest is completed, or if the giant has been slaughtered.
		else if ( Victory.IsQuestCompleted( "a1_bandits", m_Target$ )
			|| Victory.GetQuestOrder( "a1_bandits", m_Target$ ) >= 3 )

		{
			// if staff is active, use this one
			if( Victory.IsQuestActive( "a1_staff", m_Target$) )
			{
			 	m_Go$.GetConversation.RSSelectConversation( "syrd_during_a1_staff" );
			 	TryAnim$('sit3');
			 }
			 // otherwise we don't want to mention the staff of stars.
			 else
			 {
			 	m_Go$.GetConversation.RSSelectConversation( "syrd_after_a1_monsters" );
			 	TryAnim$('sit3');
			 }
		}
		
		// during bandits quest - say something crazy.
		else if ( Victory.GetQuestOrder( "a1_bandits", m_Target$ ) >= 1 )
		{
			 m_Go$.GetConversation.RSSelectConversation( "syrd_during_a1_monsters" );
			 TryAnim$('sit3');
		}
		// bandits not active -- say something crazy
		else
		{
			m_Go$.GetConversation.RSSelectConversation( "syrd_before_a1_monsters" );
			TryAnim$('sit3');
		}
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}


