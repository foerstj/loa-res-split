/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_lyssa.skrit

	Author(s)	: 	Bartosz Kijanka / Anu

	Purpose		: 	Make Lyssa (a1 NPC) talk goodly

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Face$;
}

////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		// read/write variables from the Game Auditor here, and bring up the appropriate dialogs...


	if ( Victory.GetQuestOrder( "a1_staff", m_Target$ ) >= 2 )
	{
		// THIS BLOCK IS IF THE LOST QUEEN IS DEAD
		m_Go$.GetConversation.RSSelectConversation( "lyssa_qc" );

		if ( GameAuditor.GetDb.GetBool( "party_accept_0x200191" ) ) {
			// when we hire lyssa, we give her the bow
			// check to see if the quest is active and the player has the bow
			if ( Victory.IsQuestActive( "a1_bow", m_Target$ ) && 
			   ( Goid.InValidGoid != UIPartyManager.DoesActivePartyHaveTemplate( "bw_dsx_reynards_gift", m_Target$ ) ) )
			{
				// complete the a1_bow quest
				Victory.RSActivateQuest( "a1_bow", 1, m_Target$ );
				Victory.RSCompletedQuest( "a1_bow", m_Target$ );
				
				// say bow stuff
				m_Go$.GetConversation.RSSelectConversation( "lyssa_hire_bow" );
			}
			else
			{
				// don't say bow stuff
				m_Go$.GetConversation.RSSelectConversation( "lyssa_qc_hire" );
			}
			// when we hire lyssa we take away her invincibility
			if( m_Go$.HasAspect() )
			{
				m_Go$.Aspect.SetIsInvincible(false);
			}
			GameAuditor.GetDb.SetBool( "party_accept_0x200191", false );
			TryAnim$('talk');
		} else if ( GameAuditor.GetDb.GetBool( "party_decline_0x200191" ) ) {
			m_Go$.GetConversation.RSSelectConversation( "lyssa_not_hire" );
			GameAuditor.GetDb.SetBool( "party_decline_0x200191", false );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x200191", true );
			TryAnim$('talk');
		} else if ( GameAuditor.GetDb.GetBool( "party_disband_0x200191" ) ) {
			m_Go$.GetConversation.RSSelectConversation( "lyssa_disband" );
			GameAuditor.GetDb.SetBool( "party_disband_0x200191", false );
			GameAuditor.GetDb.SetBool( "party_disbanded_0x200191", true );
			TryAnim$('talk');
		} else if ( GameAuditor.GetDb.GetBool( "party_disbanded_0x200191" ) ) {
			m_Go$.GetConversation.RSSelectConversation( "lyssa_late_hire" );			
			TryAnim$('talk');
		} else if ( GameAuditor.GetDb.GetBool( "party_allow_rejoin_0x200191" ) ) {
			m_Go$.GetConversation.RSSelectConversation( "lyssa_late_hire" );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x200191", false );
			TryAnim$('talk');
		}  else {
			TryAnim$('ulo1');
		}
	}
	else	
	{
		// THIS BLOCK IS IF THE LOST QUEEN IS *NOT* DEAD
		m_Go$.GetConversation.RSSelectConversation( "lyssa_meet" );

		if ( GameAuditor.GetDb.GetBool( "party_accept_0x200191" ) ) 
		{
			// when we hire lyssa, we give her the bow
			// check to see if the quest is active and tHe player has the bow
			if ( Victory.IsQuestActive( "a1_bow", m_Target$ ) && 
			   ( Goid.InValidGoid != UIPartyManager.DoesActivePartyHaveTemplate( "bw_dsx_reynards_gift", m_Target$ ) ) )
			{
				// complete the a1_bow quest
				Victory.RSActivateQuest( "a1_bow", 1, m_Target$ );
				Victory.RSCompletedQuest( "a1_bow", m_Target$ );
				// say bow stuff
				m_Go$.GetConversation.RSSelectConversation( "lyssa_hire_bow" );
			}
			else
			{
				// don't say bow stuff
				m_Go$.GetConversation.RSSelectConversation( "lyssa_hire" );
			}
			// when we hire lyssa we take away her invincibility
			if( m_Go$.HasAspect() )
			{
				m_Go$.Aspect.SetIsInvincible(false);
			}
			GameAuditor.GetDb.SetBool( "party_accept_0x200191", false );
			TryAnim$('talk');
		}
		else if ( GameAuditor.GetDb.GetBool( "party_decline_0x200191" ) ) 
		{
			m_Go$.GetConversation.RSSelectConversation( "lyssa_not_hire" );
			GameAuditor.GetDb.SetBool( "party_decline_0x200191", false );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x200191", true );
			TryAnim$('talk');
		}
		else if ( GameAuditor.GetDb.GetBool( "party_disband_0x200191" ) ) 
		{
			m_Go$.GetConversation.RSSelectConversation( "lyssa_disband" );
			GameAuditor.GetDb.SetBool( "party_disband_0x200191", false );
			GameAuditor.GetDb.SetBool( "party_disbanded_0x200191", true );
			TryAnim$('talk');
		}
		else if ( GameAuditor.GetDb.GetBool( "party_disbanded_0x200191" ) ) 
		{
			m_Go$.GetConversation.RSSelectConversation( "lyssa_late_hire" );			
			TryAnim$('talk');
		}
		else if ( GameAuditor.GetDb.GetBool( "party_allow_rejoin_0x200191" ) ) 
		{
			m_Go$.GetConversation.RSSelectConversation( "lyssa_late_hire" );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x200191", false );
			TryAnim$('talk');
		}
		else
		{
			//OverheadMap.RSUpdateAreaMarkerVisibility("lost_queen", true, m_Target$);
			//OverheadMap.RSUpdateAreaMarkerQuestState("lost_queen", true, m_Target$);
			TryAnim$('ulo1');
		}

	}

		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


